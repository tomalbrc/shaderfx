#define N 6
#define NN float(N)
#define INTERVAL 3.0
#define INTENSITY vec3((NN * INTERVAL - t) / (NN * INTERVAL))

vec2 foldRotate(in vec2 p, in float s) {
    float a = PI / s - atan(p.x, p.y);
    float n = PI * 2. / s;
    a = floor(a / n) * n;
    p *= mat2_rotate_z(a);
    return p;
}

float sdRect(vec2 p, vec2 b) {
    vec2 d = abs(p) - b;
    return min(max(d.x, d.y), 0.0) + length(max(d, 0.0));
}

vec2 hash2(vec2 p) {
    p = vec2(dot(p, vec2(127.1, 311.7)), dot(p, vec2(269.5, 183.3)));
    return fract(sin(p) * 43758.5453) * 2.0 - 1.0;
}

float Bokeh(vec2 p, vec2 sp, float size, float mi, float blur) {
    float d = length(p - sp);
    float c = smoothstep(size, size*(1.-blur), d);
    c *= mix(mi, 1., smoothstep(size*0.8, size, d));
    return c;
}

float dirt(vec2 uv, float n) {
    vec2 p = fract(uv * n);
    vec2 st = (floor(uv * n) + 0.5) / n;
    vec2 rnd = hash2(st);
    return Bokeh(p, vec2(0.5, 0.5) + vec2(0.2) * rnd, 0.05, abs(rnd.y * 0.4) + 0.3, 0.25 + rnd.x * rnd.y * 0.2);
}

float tex(vec2 p, float z) {
    p = foldRotate(p, 8.0);
    vec2 q = (fract(p / 10.0) - 0.5) * 10.0;
    for (int i = 0; i < 3; ++i) {
        for(int j = 0; j < 2; j++) {
            q = abs(q) - 0.25;
            q *= mat2_rotate_z(PI * 0.25);
        }
        q = abs(q) - vec2(1.0, 1.5);
        q *= mat2_rotate_z(PI * 0.25 * z);
        q = foldRotate(q, 3.0);
    }
    float d = sdRect(q, vec2(1., 1.));
    float f = 1.0 / (1.0 + abs(d));
    return smoothstep(0.9, 1., f);
}

float sm(float start, float end, float t, float smo) {
    return smoothstep(start, start + smo, t) - smoothstep(end - smo, end, t);
}

vec4 fractal1() {
    vec2 uv = gl_FragCoord.xy / ScreenSize.xy;
    uv = uv * 2.0 - 1.0;
    uv.x *= ScreenSize.x / ScreenSize.y;
    uv *= 2.0;

    float time = GameTime * 1000.0;

    vec3 col = vec3(0.0);
    for(int i = 0; i < N; i++) {
        float t;
        float ii = float(N - i);

        t = ii * INTERVAL - mod(time - INTERVAL * 0.75, INTERVAL);
        col = mix(col, INTENSITY, dirt(mod(uv * max(0.0, t) * 0.1 + vec2(0.2, -0.2) * time, 1.2), 3.5));

        t = ii * INTERVAL - mod(time + INTERVAL * 0.5, INTERVAL);
        col = mix(col, INTENSITY * vec3(0.7, 0.8, 1.0) * 1.3, tex(uv * max(0.0, t), 4.45));

        t = ii * INTERVAL - mod(time - INTERVAL * 0.25, INTERVAL);
        col = mix(col, INTENSITY * vec3(1.0), dirt(mod(uv * max(0.0, t) * 0.1 + vec2(-0.2, -0.2) * time, 1.2), 3.5));

        t = ii * INTERVAL - mod(time, INTERVAL);
        float r = length(uv * 2.0 * max(0.0, t));
        float rr = sm(-24.0, -0.0, (r - mod(time * 30.0, 90.0)), 10.0);
        col = mix(col, mix(INTENSITY * vec3(1.0), INTENSITY * vec3(0.7, 0.5, 1.0) * 3.0, rr), tex(uv * 2.0 * max(0.0, t), 0.27 + (2.0 * rr)));
    }

    return vec4(col, 1.0);
}